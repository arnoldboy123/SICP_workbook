## Exercise 1.29

```scheme
#lang scheme
(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a) (sum term (next a) next b))))

(define (simpson-rule f a b n)
  (define h (/ (- b a) n))

  (define (inner-func k) (f (+ a (* k h))))

  (define (factor k)
    (cond ((= k 0) 1)
          ((= k n) 1)
          ((= (modulo k 2) 0) 2)
          (else 4)))

  (define (inc k) (+ k 1))

  (define (term k)
    (* (factor k) (inner-func k)))

  (* (/ h 3) (sum term 0 inc n)))

(define (cube x) (* x x x))

(simpson-rule cube 0.0 1 100)

(simpson-rule cube 0.0 1 1000)
```

The result from simpson-rule with n = 100 is `0.24999999999999992`
compared to the previous the approximation from the text book which gave an answer of `0.24998750000000042` and also has 101 summations, we can see that simpson rule gave a much more accurate result, using the same sum function

The result from simpson-rule with n = 1000 is `0.2500000000000003`, with 1001 summations, this is in contrast to the approximation from the text book which gave `0.249999875000001`. In this exercise we illustrated the power of passing in procedure as abstraction and increasing the accuracy of results by magnitude all while using the same outer function